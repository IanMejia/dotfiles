pkgname=st
pkgver=0.8.4
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
url=https://st.suckless.org
source=(https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
        terminfo.patch
        README.terminfo.rst
        ligatures.diff
        scroll.diff
        alpha.diff
        font2.diff)
sha256sums=(d42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6
            f9deea445a5c6203a0e8e699f3c3b55e27275f17fb408562c4dd5d649edeea23
            0ebcbba881832adf9c98ce9fe7667c851d3cc3345077cb8ebe32702698665be2
            a7b3681d849fd8cca75cd38022a14fef2e26dc085bed1ebd8dcf0a65baa14898
            1e41fe17a5ef5a8194eea07422b49d815e2c2bb4d58d84771f793be423005310
            9c5b4b4f23de80de78ca5ec3739dc6ce5e7f72666186cf4a9c6b614ac90fb285
            7279c787dba23d72a8a0a4613c0917e03d0087f0254f56a530cd9c521857d20b)
_sourcedir=$pkgname-$pkgver
_makeopts="--directory=$_sourcedir"

prepare() {
  patch --directory="$_sourcedir" --strip=0 < terminfo.patch
  patch --directory="$_sourcedir" < scroll.diff
  patch --directory="$_sourcedir" < alpha.diff
  patch --directory="$_sourcedir" < font2.diff
  patch --directory="$_sourcedir" < ligatures.diff

  if [ -e "$BUILDDIR/config.h" ]; then
    cp "$BUILDDIR/config.h" "$_sourcedir"
  fi
  #cp "$_sourcedir/config.def.h" "$BUILDDIR"
}

build() {
  make $_makeopts X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/$pkgname"
  local docdir="$shrdir/doc/$pkgname"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$docdir" README.terminfo.rst
  install $installopts "$shrdir/$pkgname" "$_sourcedir/st.info"
}
